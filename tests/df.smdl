#smdl

import ::df::*;
import ::math::*;

unit_test "Specular half vector for reflection" {
  const auto wo = math::normalize(float3(-0.4, 1.2, 1.6));
  const auto wi = math::normalize(float3(+0.1, -0.7, 0.4));
  const auto wm = math::normalize(df::specular::reflectionHalfVector(wo, wi));
  #assert(#all(df::specular::reflect(wo, wm) ~== [1e-5] wi));
  #assert(#all(df::specular::reflect(wi, wm) ~== [1e-5] wo));
}

unit_test "Specular half vector for refraction" {
  const auto ior = 1 / 1.4;
  const auto wo = math::normalize(float3(-0.4, 1.2, 1.6));
  const auto wi = math::normalize(float3(+0.1, -0.7, -0.4));
  const auto wm = math::normalize(df::specular::refractionHalfVector(wo, wi, ior));
  #assert(#all(df::specular::refract(wo, wm, ior) ~== [1e-5] wi));
  #assert(#all(df::specular::refract(wi, wm, 1 / ior) ~== [1e-5] wo));
}

@(pure foreign)
double erf(double x);

unit_test "Erf inverse" {
  #assert(df::monte_carlo::erfInverse(erf(-1.1d)) ~== [1e-5d] -1.1d);
  #assert(df::monte_carlo::erfInverse(erf(-0.3d)) ~== [1e-5d] -0.3d);
  #assert(df::monte_carlo::erfInverse(erf(+1.2d)) ~== [1e-5d] +1.2d);
  #assert(df::monte_carlo::erfInverse(erf(+2.7d)) ~== [1e-5d] +2.7d);
}

@(pure)
auto runLowDiscrepancyTests(auto xi) {
  auto xi_av(xi);
  for (int i = 0; i < 5000; i++) {
    xi_av += df::monte_carlo::nextLowDiscrepancy(&xi);
  }
  xi_av /= 5000;
  #assert(#all(xi_av ~== [1e-3] 0.5));
}

unit_test "Low discrepancy 2D" {
  runLowDiscrepancyTests(float2(0.1, 0.2));
}

unit_test "Low discrepancy 3D" {
  runLowDiscrepancyTests(float3(0.1, 0.2, 0.3));
}

unit_test "Low discrepancy 4D" {
  runLowDiscrepancyTests(float4(0.1, 0.2, 0.3, 0.4));
}

unit_test "Cosine-weighted hemisphere sample" {
  auto xi(float2(0.5, 0.7));
  auto wi_av(float3(0.0));
  for (int i = 0; i < 5000; i++, df::monte_carlo::nextLowDiscrepancy(&xi)) {
    wi_av += df::monte_carlo::cosineHemisphereSample(xi);
  }
  wi_av /= 5000;
  #assert(#all(wi_av ~== [1e-3] float3(0.0, 0.0, 0.666)));
}

// Integrate Smith slope PDF by transforming the open square `(-1, 1)^2` 
// into the entire plane `(-inf, inf)^2`.
@(pure)
float integrateSmithSlopePDF(const df::microfacet::distribution distr) {
  double fsum = 0;
  const int n = 512;
  for (int ix = 0; ix < n; ix++) {
    const float tx = math::lerp(-0.99999, 0.99999, ix / float(n - 1));
    const float mx = tx / (1 - tx * tx);
    double ftmp = 0;
    for (int iy = 0; iy < n; iy++) {
      const float ty = math::lerp(-0.99999, 0.99999, iy / float(n - 1));
      const float my = ty / (1 - ty * ty);
      ftmp += 2 * (1 + ty * ty) / #pow(1 - ty * ty, 2) * df::microfacet::smithSlopePDF(distr, float2(mx, my));
    }
    fsum += 2 * (1 + tx * tx) / #pow(1 - tx * tx, 2) * ftmp / n;
  }
  return fsum / n;
}

// Integrate Smith normal PDF by cosine-weighted hemisphere sampling.
@(pure)
float integrateSmithNormalPDF(const df::microfacet::distribution distr, const float2 alpha) {
  double fsum = 0;
  const int n = 512 * 512;
  float2 xi = float2(0.228, 0.712);
  for (int i = 0; i < n; i++, df::monte_carlo::nextLowDiscrepancy(&xi)) {
    fsum += df::microfacet::smithNormalPDF(distr, alpha, df::monte_carlo::cosineHemisphereSample(xi));
  }
  return $PI * fsum / n;
}

unit_test "Microfacet distributions" {
  #assert(integrateSmithSlopePDF(df::microfacet::distribution_ggx()) ~== [1e-2] 1.0);
  #assert(integrateSmithSlopePDF(df::microfacet::distribution_beckmann()) ~== [1e-2] 1.0);
  #assert(integrateSmithNormalPDF(df::microfacet::distribution_ggx(), float2(0.8, 1.2)) ~== [1e-2] 1.0);
  #assert(integrateSmithNormalPDF(df::microfacet::distribution_beckmann(), float2(1.1, 0.7)) ~== [1e-2] 1.0);
}

struct evaluate_result {
  color f = color(0);

  float2 pdf = float2(0);
};

evaluate_result evaluate(const bsdf scattering, float3 wo, float3 wi) {
  auto mat = material(surface: material_surface(scattering: scattering));
  auto instance = _material_instance(&mat);
  float pdf_fwd = 0;
  float pdf_rev = 0;
  color f = color(0);
  if (df::_scatterEvaluate(&instance, &wo, &wi, &pdf_fwd, &pdf_rev, cast<&float>(&f)))
    return evaluate_result(f, float2(pdf_fwd, pdf_rev));
  else
    return evaluate_result();
}

unit_test "Reciprocity" {
  const auto wo = math::normalize(float3(-0.4, 1.2, 0.2));
  const auto wi = math::normalize(float3(+0.1, -0.7, 0.4));
  const auto f = df::weighted_layer(
    weight: 0.5,
    layer: df::sheen_bsdf(tint: 0.8, roughness: 0.6),
    base: df::fresnel_layer(
      ior: 1.4,
      layer: df::weighted_layer(
        weight: 0.5,
        layer: df::ward_geisler_moroder_bsdf(roughness_u: 0.4, roughness_v: 0.8, tint: 1.0, multiscatter_tint: 1.0),
        base: df::microfacet_ggx_smith_bsdf(roughness_u: 0.2, roughness_v: 0.5, tint: 1.0, multiscatter_tint: 1.0),
      ),
      base: df::diffuse_reflection_bsdf(tint: 0.8, roughness: 0.6),
    ),
  );
  const auto result0 = evaluate(f, wo, wi);
  const auto result1 = evaluate(f, wi, wo);
  #assert(#all(#abs(result0.pdf - result1.pdf.yx) ~== [1e-6] 0.0));
  #assert(#all(#abs(result0.f / #abs(wi.z) - result1.f / #abs(wo.z)) ~== [1e-6] 0.0));
}

/*
unit_test "Phase" {
  auto wo = math::normalize(float3(-1.0, 0.0, 0.0));
  auto wi = math::normalize(float3(+1.0, -0.0, 0.0));
  auto mat = material(volume: material_volume(scattering: df::anisotropic_vdf(directional_bias: 0.5)));
  auto instance = _material_instance(&mat);
  #print(df::_volumeScatterEvaluate(&instance, &wo, &wi), "\n");
  #print(df::_volumeScatterEvaluate(&instance, &wo, &wo), "\n");
}
 */
